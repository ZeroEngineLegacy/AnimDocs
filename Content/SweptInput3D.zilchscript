//////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright 2017 DigiPen USA Corporation, All Rights Reserved
// Author: Zero Engine Team
// Last updated by: Joshua Davis
// Last update: 7/24/2017
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

// Simple input for a 3d swept character controller. Rotation is controlled by the child camera.
class SweptInput3D : ZilchComponent
{
  [Dependency] var SweptController : SweptController;
  [Dependency] var Orientation : Orientation;
  
  [Property] var LeftKey : Keys = Keys.A;
  [Property] var RightKey : Keys = Keys.D;
  [Property] var ForwardKey : Keys = Keys.W;
  [Property] var BackKey : Keys = Keys.S;
  [Property] var JumpKey : Keys = Keys.Space;
  
  sends JumpEvent : JumpEvent;
  sends DoubleJumpEvent : JumpEvent;
  sends MovementEvent : MovementEvent;
  
  var PreJump : Boolean = false;
  
  [Property]
  var ModelObj : CogPath = CogPath();
  
  function Initialize(init : CogInitializer)
  {
    Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
    Zero.Connect(this.ModelObj.Cog, Events.AnimationEnded, this.OnAnimationEnded);
  }

  function OnAnimationEnded(event : AnimationGraphEvent)
  {
    if(event.Animation == this.ModelObj.Cog.BlendedAnimationController.JumpStartAnimation)
    {
      this.PreJump = false;
      this.SweptController.Jump();
    }
  }

  function OnLogicUpdate(event : UpdateEvent)
  {
    // Accumulate movement
    var movement = Real3(0, 0, 0);
    if(Zero.Keyboard.KeyIsDown(this.LeftKey))
      movement -= this.Orientation.WorldRight;
    else if(Zero.Keyboard.KeyIsDown(this.RightKey))
      movement += this.Orientation.WorldRight;
    if(Zero.Keyboard.KeyIsDown(this.ForwardKey))
      movement += this.Orientation.WorldForward;
    else if(Zero.Keyboard.KeyIsDown(this.BackKey))
      movement -= this.Orientation.WorldForward;
    
    if(this.PreJump)
      movement = Real3();
    
    this.SweptController.Update(movement, event.Dt);
    this.Owner.DispatchDown(Events.MovementEvent, MovementEvent(movement));
    
    if(Zero.Keyboard.KeyIsPressed(this.JumpKey))
    {
      if(this.SweptController.Grounded)
      {
        Console.WriteLine("Jump Input");
        this.Owner.DispatchDown(Events.JumpEvent, JumpEvent());
        this.PreJump = true;
      }
      else
      {
        Console.WriteLine("Double Jump Input");
        this.Owner.DispatchDown(Events.DoubleJumpEvent, JumpEvent());
        this.SweptController.JumpUnconditionally();
      }
    }
  }
}


class JumpEvent : ZilchEvent {}
class MovementEvent : ZilchEvent
{
  var MoveDir : Real3;
  
  constructor(dir : Real3) : base()
  {
    this.MoveDir = dir;
  }
}